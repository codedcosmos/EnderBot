plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'codedcosmos'
version '1.1'
mainClassName = 'codedcosmos.enderbot.core.EnderBot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
}

sourceSets {
    main.resources.srcDirs = ['res']
    main.java.srcDirs = ['src']
}

dependencies {
    compile ('net.dv8tion:JDA:4.1.1_101') {
        exclude module: 'opus-java'
    }

    compile 'com.google.api-client:google-api-client:1.23.0'
    compile 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
    compile 'com.google.apis:google-api-services-drive:v3-rev110-1.23.0'

    compileOnly 'org.spigotmc:spigot:1.15.2-R0.1-SNAPSHOT'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from {
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }

        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}



task runDir(dependsOn: classes, type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in

    workingDir = new File('../run')
}